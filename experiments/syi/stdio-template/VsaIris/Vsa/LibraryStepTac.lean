import VsaIris.Vsa.AllocTac
import VsaIris.Vsa.ProofPieces

namespace VsaIris.Sym

open Lean Elab Tactic Meta

theorem mem_accAddrs_iff {a w b : Nat} : b ∈ accAddrs a w ↔ a ≤ b ∧ b < a + w :=
  ⟨of_mem_accAddrs, fun ⟨h1, h2⟩ => by
    have e : a + (b - a) = b := by omega
    rw [← e]; exact mem_accAddrs (by omega)⟩

macro_rules
  | `(tactic| sx_side) =>
    `(tactic| (intro b hb; simp only [mem_accAddrs_iff, List.mem_append, VsaIris.InExt] at *; sx_addr))

syntax "ix_mem" : tactic
macro_rules
  | `(tactic| ix_mem) =>
    `(tactic| simp (disch := sx_addr) only [ldv_store_hit, ldv_ld_hit_eq, ldv_ld_miss] at *)

macro_rules
  | `(tactic| sx_side) => `(tactic| decide)

private def hex8 (n : Nat) : String :=
  let s := String.ofList (Nat.toDigits 16 n)
  String.ofList (List.replicate (8 - s.length) (Char.ofNat 48)) ++ s

def ixNormTab (facts : Array Term) (tab : Option (TSyntax `tactic)) : TacticM Syntax := do
  let tab ← match tab with
    | some t => pure t
    | none => `(tactic| skip)
  let tab : TSyntax ``Lean.Parser.Tactic.tacticSeq ← `(Lean.Parser.Tactic.tacticSeq| $tab:tactic)
  if facts.isEmpty then
    `(tactic| ((try sx_norm) <;> (try $tab) <;> (try sx_norm) <;> (try ix_mem)))
  else
    let lems : Array (TSyntax `Lean.Parser.Tactic.simpLemma) ←
      facts.mapM fun f => `(Lean.Parser.Tactic.simpLemma| $f:term)
    `(tactic| ((try sx_norm) <;> (try simp only [$lems,*]) <;> (try $tab) <;> (try sx_norm) <;> (try ix_mem)))

def ixNorm (facts : Array Term) : TacticM Syntax := ixNormTab facts none

def ixSide (side : Option Syntax) : TacticM Syntax := do
  match side with
  | some t => pure t
  | none => `(tactic| sx_side)

def ixTrySide (norm : Syntax) (g : MVarId) (side : Option Syntax := none) : TacticM Bool := do
  let saved ← saveState
  let sd ← ixSide side
  try
    let gs ← evalTacticAt (← `(tactic| ($(⟨norm⟩) <;> $(⟨sd⟩)))) g
    if gs.isEmpty then return true
    saved.restore; return false
  catch _ =>
    saved.restore; return false

def ixTryPrune (norm : Syntax) (g : MVarId) (side : Option Syntax := none)
    (condFacts : Bool := false) : TacticM Bool := do
  let saved ← saveState
  let sd ← ixSide side
  try

    let tac ← if condFacts then
        `(tactic| (intro hc; exfalso; revert hc; (try simp only [VsaIris.Sym.upd_apply, Nat.reduceEqDiff, ite_true, ite_false, ne_eq, Decidable.not_not]); ($(⟨norm⟩) <;> $(⟨sd⟩))))
      else `(tactic| (intro hc; exfalso; ($(⟨norm⟩) <;> (revert hc; $(⟨sd⟩)))))
    let gs ← evalTacticAt tac g
    if gs.isEmpty then return true
    saved.restore; return false
  catch _ =>
    saved.restore; return false

def ixPre : List String := ["it", "itD", "itT", "itH", "itO", "itS", "itDS", "itTS", "itHS", "itOS"]

def ixCandidates (pc : Nat) (pre : List String := ixPre) :
    TacticM (List Name) := do
  let env ← getEnv
  let mk (p : String) := Name.mkStr (Name.mkStr (Name.mkStr .anonymous "VsaIris") "Sym") s!"{p}_{hex8 pc}"
  return (pre.map mk).filter env.contains

def ixApply (norm : Syntax) (h : Syntax) (g : MVarId) (nm : Name) (strict : Bool)
    (side : Option Syntax := none) : TacticM (Option (List MVarId × List MVarId)) := do
  let saved ← saveState
  try
    let gs ← evalTacticAt (← `(tactic| apply $(mkIdent nm) $(⟨h⟩))) g
    let mut conts : List MVarId := []
    let mut pending : List MVarId := []
    for g in gs do
      let ty ← g.withContext (do instantiateMVars (← g.getType))
      if ← g.withContext (forallTelescopeReducing ty fun _ b => isSWP b) then
        conts := conts ++ [g]
      else if !(← ixTrySide norm g side) then
        pending := pending ++ [g]
    if strict && !pending.isEmpty then
      saved.restore; return none
    return some (conts, pending)
  catch _ =>
    saved.restore; return none

def ixStep (norm : Syntax) (h : Syntax) (g : MVarId)
    (pre : List String := ixPre) (side : Option Syntax := none) :
    TacticM (Option (List MVarId × List MVarId)) := do
  let some pc ← g.withContext (do swpPC? (← g.getType)) | return none
  let cands ← ixCandidates pc pre
  for nm in cands do
    if let some r ← ixApply norm h g nm true side then return some r
  for nm in cands do
    if let some r ← ixApply norm h g nm false side then return some r
  return none

syntax "ix_run " ("[" num "] ")? term (" using " "[" term,* "]")? (" at " num+)? : tactic

syntax "ix_run1 " ("[" num "] ")? term (" using " "[" term,* "]")? (" at " num+)? : tactic

def ixRunCore (explore : Bool) (n : Option (TSyntax `num)) (h : Syntax)
    (fs : Option (Syntax.TSepArray `term ",")) (stops : Option (Array (TSyntax `num)))
    (pre : List String := ixPre)
    (mkNorm : Array Term → TacticM Syntax := ixNorm) (clearPruned : Bool := false)
    (budgetPct : Nat := 0) (condFacts : Bool := false) :
    TacticM Unit := do
    let budget := (n.map (·.getNat)).getD 400
    let stopPCs : List Nat := match stops with
      | some ss => ss.toList.map (·.getNat)
      | none => []
    let facts : Array Term := match fs with
      | some fs => fs.getElems
      | none => #[]
    let norm ← mkNorm facts
    let mut pending : List MVarId := []
    let mut stuck : List MVarId := []
    let first ← getMainGoal

    let first ← do
      let saved ← saveState
      try
        match ← evalTacticAt (← `(tactic| (try simp only [Nat.reduceAdd]))) first with
        | [c'] => pure c'
        | _ => saved.restore; pure first
      catch _ => saved.restore; pure first

    let mut work : List (MVarId × Nat) := [(first, budget)]
    let ctx ← readThe Core.Context
    while !work.isEmpty do
      let (cur, fuel) := work.head!
      work := work.tail!
      if fuel == 0 then stuck := stuck ++ [cur]; continue

      if budgetPct != 0 && ctx.maxHeartbeats != 0 then
        let used := (← IO.getNumHeartbeats) - ctx.initHeartbeats
        if used * 100 > ctx.maxHeartbeats * budgetPct then stuck := stuck ++ [cur]; continue
      if let some pc ← cur.withContext (do swpPC? (← cur.getType)) then
        if stopPCs.contains pc then stuck := stuck ++ [cur]; continue
      let some (conts, pend) ← ixStep norm h cur pre | stuck := stuck ++ [cur]; continue
      pending := pending ++ pend
      match conts with
      | [c] =>

        let c ← do
          let ty ← c.withContext (do whnfR (← instantiateMVars (← c.getType)))
          if ty.isForall then
            match ← evalTacticAt (← `(tactic| intro _)) c with
            | [c'] => pure c'
            | _ => pure c
          else pure c
        let c ← do
          let saved ← saveState
          try
            match ← evalTacticAt norm c with
            | [c'] => pure c'
            | _ => saved.restore; pure c
          catch _ => saved.restore; pure c
        if (← c.withContext (do swpPC? (← c.getType))).isSome then
          work := (c, fuel - 1) :: work
        else
          stuck := stuck ++ [c]
      | [t, f] =>
        let introTac ← if clearPruned then `(tactic| intro _) else `(tactic| intro hc)
        if ← ixTryPrune norm t none condFacts then
          let [f'] ← evalTacticAt introTac f | stuck := stuck ++ [f]; continue
          work := (f', fuel - 1) :: work
        else if ← ixTryPrune norm f none condFacts then
          let [t'] ← evalTacticAt introTac t | stuck := stuck ++ [t]; continue
          work := (t', fuel - 1) :: work
        else if !explore then
          stuck := stuck ++ [t, f]
        else

          let [t'] ← evalTacticAt (← `(tactic| intro hc)) t | stuck := stuck ++ [t, f]; continue
          let [f'] ← evalTacticAt (← `(tactic| intro hc)) f | stuck := stuck ++ [t', f]; continue
          work := (t', fuel - 1) :: (f', fuel - 1) :: work
      | cs => stuck := stuck ++ cs
    setGoals (pending ++ stuck)

elab_rules : tactic
  | `(tactic| ix_run $[[$n]]? $h $[using [$fs,*]]? $[at $stops*]?) => ixRunCore true n h fs stops
  | `(tactic| ix_run1 $[[$n]]? $h $[using [$fs,*]]? $[at $stops*]?) => ixRunCore false n h fs stops

end VsaIris.Sym
